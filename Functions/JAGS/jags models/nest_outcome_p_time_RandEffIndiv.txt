model {

# Priors and constraints
phi.b0 ~ dnorm(0, 1e-5)
phi.b1 <- 0
p.b0 ~ dnorm(0, 1e-5)
p.b1 ~ dnorm(0, 1e-5)
sigma ~ dunif(0,5)
tau <- pow(sigma,-2)
sigma2 <- pow(sigma, 2)

for(i in 1:nests) {
for (t in 1:(days-1)){
    logit(phi[i,t]) <- phi.b0 + phi.b1 * t + epsilon[i]
    logit(p[i,t]) <- p.b0 + p.b1 * t 
  }#i
}#t

for(i in 1:nests) {
    epsilon[i] ~ dnorm(0, tau)
}

# Likelihood
for (i in 1:nests){
   # Define latent state at first capture
   z[i, 1] <- 1
   for (t in 2:days){
      # State process
      mu1[i, t] <- phi[i,t-1] * z[i, t-1]
      z[i, t] ~ dbern(mu1[i, t])

      # Observation process
      mu2[i, t] <- p[i,t-1] * z[i, t]
      y[i, t] ~ dbin(mu2[i, t], gps_fixes[i, t])
      } #t
   } #i
}
